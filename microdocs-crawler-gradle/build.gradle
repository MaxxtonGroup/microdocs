
apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile gradleApi()
    compile ("com.fasterxml.jackson.core:jackson-databind:2.7.5")
    compile 'org.codehaus.groovy:groovy-all:2.4.4'
    compile ("com.maxxton.microdocs:microdocs-core-java:1.0")
}

group='com.maxxton'
version = '1.0'

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'MicroDocs Gradle Crawler'
                packaging 'jar'
                description 'MicroDocs crawler for gradle projects'
                url 'https://github.com/MaxxtonGroup/microservice-documentation-tool'

                scm {
                    url 'scm:git@github.com:MaxxtonGroup/microservice-documentation-tool.git'
                    connection 'scm:git@github.com:MaxxtonGroup/microservice-documentation-tool.git'
                    developerConnection 'scm:git@github.com:MaxxtonGroup/microservice-documentation-tool.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 's.hermans'
                        name 'Steven Hermans'
                    }
                }
            }
        }
    }
}