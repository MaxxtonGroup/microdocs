
apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile gradleApi()
    compile ("com.fasterxml.jackson.core:jackson-databind:2.7.5")
    compile ("com.mashape.unirest:unirest-java:1.4.9")
    compile ("org.codehaus.groovy:groovy-all:2.4.4")
    compile ("com.maxxton:microdocs-core-java:1.6.2")
}

group='com.maxxton'
version = '1.6.2'

task fatJar(type: Jar, dependsOn: 'jar') {
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    include('com/maxxton/**')
    include('com/fasterxml/**')
    include('com/mashape/**')
    include('META-INF/gradle-plugins/microdocs.properties')
    include('microdocs-crawler-doclet.jar')
    with jar
}
manifest {
    attributes 'Implementation-Title': 'microdocs-crawler-gradle',
            'Implementation-Version': version
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives fatJar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
//            repository(url: mavenLocal().url)

            pom.project {
                name 'MicroDocs Gradle Crawler'
                packaging 'jar'
                description 'MicroDocs crawler for gradle projects'
                url 'https://github.com/MaxxtonGroup/microdocs'

                scm {
                    url 'scm:git@github.com:MaxxtonGroup/microdocs.git'
                    connection 'scm:git@github.com:MaxxtonGroup/microdocs.git'
                    developerConnection 'scm:git@github.com:MaxxtonGroup/microdocs.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 's.hermans'
                        name 'Steven Hermans'
                    }
                }
            }
        }
    }
}